/**
    The script moves the piece horizontally in such a way that allows the piece
    to go deepest.
    It doesn't use rotations.
 */

length = 20;
width = 10;

grid = world.getCurrentGrid();
piece = world.getCurrentPiece();

int getLength(int[][] piece) {
    for(int i=3; i>=0; i--) {
        boolean populated = false;
        for(int j=0; j<4; j++)
            if(piece[i][j] == 1) populated = true;
        if(populated) return i+1;
        }
    return 0;
}

int getWidth(int[][] piece) {
    int[] rowMax = new int[4];
    int max = 0;
    for(int i=0; i<4; i++) {
        for(int j=3; j>=0; j--)
            if(piece[i][j] == 1) {
                rowMax[i] = j+1;
                break;
                }
        if(rowMax[i] > max) max = rowMax[i];
        }
    return max;
}

pieceLength = getLength(piece);
pieceWidth = getWidth(piece);

boolean allowed (int x, int y) {
    if(x+pieceLength > length) return false;
    if(y+pieceWidth > width) return false;
    for(int i=0; i<pieceLength; i++)
        for(int j=0; j<pieceWidth; j++)
            if(piece[i][j] == 1 && grid[x+i][y+j] == 1) return false;
    return true;
}

max = 0;
best = 0;
for(int i=0; i<width; i++) {
    j=0;
    while(allowed(j,i)) j++;
    j--;
    if(j>max) {
        max = j;
        best = i;
        }
    }

if(best > 4) world.moveRight(best-4);
if(best < 4) world.moveLeft(4-best);
world.dropPiece();

Thread.sleep(500);